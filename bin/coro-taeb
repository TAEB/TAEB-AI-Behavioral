#!/usr/bin/env perl
use strict;
use warnings;
use lib 'lib';

use Continuity;
use Curses;
use TAEB;
use TAEB::Interface::Local;
use TAEB::AI::Personality::Hivemind;

TAEB->personality(TAEB::AI::Personality::Hivemind->new);
TAEB->interface(TAEB::Interface::Local->new);

$| = 1;

initscr;
END { endwin }
noecho;
cbreak;
start_color;
use_default_colors;
init_pair($_, $_, 0) for 0 .. 8;

addstr "Loading TAEB...";

Continuity->new(port => 8011, path_session => 1, debug_level => 0)->loop;

our $request;
sub main {
    my $new_request = shift;

    my $path = $new_request->uri->path;
    TAEB->info("New request at $path.");

    if ($path =~ m{^/ajax-tile}) {
        my $id = $new_request->param('id');
        my ($x, $y) = $id =~ /^tile-(\d+)-(\d+)$/;
        my $tile = TAEB->current_level->at($x, $y);
        my $info = TAEB::AI::Personality::Hivemind::Templates::tile($tile);
        $new_request->print($info);
        return;
    }

    return if $request;
    $request = $new_request;

    # XXX: otherwise Coro or Continuity spews out tons of shit
    close STDERR;
    local $SIG{__WARN__} = sub { TAEB->warning(@_) };
    local $SIG{__DIE__} = sub {

        unless ("@_" =~ /Game over, man/) {
            TAEB->error("@_");
            TAEB->save;
        }

        die @_;
    };

    while (1) {
        TAEB->iterate;
    }
}

